(ns zo3ja\config
  (:use Phel\Compiler\CompilerFacade))

(def *feeds* (var ["https://zenn.dev/hanhan1978/feed"]))

(def *cache-seconds* (var (* 60 60)))

(defstruct config [envs feeds cache-seconds])

(defn create-config [envs]
  (config envs (deref *feeds*) (deref *cache-seconds*)))

(defn config-filename [config]
  (let [envs (config :envs)]
    (cond
      (envs "XDG_CONFIG_HOME") (format "%s/zo3ja/zo3ja-config.phel" (envs "XDG_CONFIG_HOME"))
      (envs "HOME") (format "%s/.config/zo3ja/zo3ja-config.phel" (envs "HOME"))
      true "zo3ja-config.phel")))

(defn config-user-config [config]
  (let [filename (config-filename config)]
    (if (php/file_exists filename)
      (let [content (php/file_get_contents filename)
            cf (php/new CompilerFacade)]
        (try
          (php/-> cf (eval content))
          (catch \Throwable e
            (println
              (format "\033[31mERROR\033[m: fail to read user config file. %s" (php/-> e (getMessage))))))
        (create-config (config :envs)))
      config)))
